/* 매크로 변수 설정 */
%INCLUDE "/SASDATA/STP/defect_image_function.sas";
/* 불량 TYPE 구분(CUSTOMER_NAME DEFECT_NAME ) */
%fun_DEFECT_TYPE_SELECTION;

/* 불량 CODE 매크로 변수 생성 */
PROC SQL NOPRINT;
    SELECT DISTINCT
        &g_defect_code
    INTO
        : DEFECT_CODE
    FROM VA_DATA.VA_V_DEFECTIMAGEINFO
    WHERE 1=1
    AND MACHINE_CODE = "&MACHINE_CODE"
    AND LOT_NO = "&LOT_NO"
    AND &g_defect_name = "&DEFECT_NAME"
    AND START_DATE BETWEEN &START_DATE AND &END_DATE
;
QUIT;

/* 불량 이미지 검색 STP(DEFECT_IMG_SEARCH) Parameter 데이터 생성 */
DATA image_param_data;
    LENGTH name $50. value $50. ;
    /*1*/name = "MACHINE_CODE" ;    value = "&MACHINE_CODE";    OUTPUT;
    /*2*/name = "LOT_NO" ;          value = "&LOT_NO";          OUTPUT;
    /*3*/name = "UNIT_RANGE" ;      value = "&UNIT_RANGE";      OUTPUT;
    /*4*/name = "DEFECT_TYPE" ;     value = "&DEFECT_TYPE";        OUTPUT;
    /*5*/name = "DEFECT_CODE" ;     value = "&DEFECT_CODE";        OUTPUT;
    /*6*/name = "START_DATE" ;     value = "&START_DATE";        OUTPUT;
    /*7*/name = "END_DATE" ;     value = "&END_DATE";            OUTPUT;
RUN;

/* Parameter 별 매크로 변수 생성 */
PROC SQL;
    SELECT
          name as param_name
        , value as param_value
    into
          :l_param_name_1-:l_param_name_&SysMaxLong
        , :l_param_value_1-:l_param_value_&SysMaxLong
    FROM work.image_param_data
    ;
QUIT;


/* SAS STP Url 링크 */
DATA _null_;
    FILE _webout;
    input line : $1000. ;
    line = resolve(_infile_) ;
    put line;
    cards4 ;
<html>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
<script type="text/JavaScript">
function win_popup() {
window.open(
"&sas_url/SASStoredProcess/do?_program=&stp_search_path%nrstr(&)&l_param_name_1=&l_param_value_1%nrstr(&)&l_param_name_2=&l_param_value_2%nrstr(&)&l_param_name_3=&l_param_value_3%nrstr(&)&l_param_name_4=&l_param_value_4%nrstr(&)&l_param_name_5=&l_param_value_5%nrstr(&)&l_param_name_6=&l_param_value_6%nrstr(&)&l_param_name_7=&l_param_value_7"
,"show", "width=1800, height=800, toolbar=no, menubar=no" ) ;
setTimeout(function(){top.window.opener = top;top.window.open('','_parent','');top.window.close();});
}
</script>
</head>
<body onload="win_popup();">
<form  id="img_stp" name="img_stp" >
</form>
</body>
</html>
;;;;
RUN;
