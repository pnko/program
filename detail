/* 매크로 변수 설정 */
%INCLUDE "/SASDATA/STP/defect_image_function.sas";
/* 불량 TYPE 구분(CUSTOMER_NAME DEFECT_NAME ) */
%fun_DEFECT_TYPE_SELECTION;

/* 카메라 번호 0 만들어 주기 */
DATA _NULL_;
	IF &CAM_NO < 10 THEN DO;
	   CALL SYMPUTX("CAM_NO_ZERO", CATS("0","&CAM_NO"));
	END;
    ELSE DO;
	   CALL SYMPUTX("CAM_NO_ZERO", CATS("0","&CAM_NO"));
    END;
RUN;

/* 기준 데이터 생성 */
DATA IMAGE_DATA_TEMP ;
SET VA_DATA.VA_V_DEFECTIMAGEINFO(WHERE=(
    MACHINE_CODE = "&MACHINE_CODE"
    AND LOT_NO  = "&LOT_NO"
    AND UNIT_RANGE =  "&UNIT_RANGE"
    AND CAM_NO =  "&CAM_NO_ZERO"
    AND (START_DATE BETWEEN &START_DATE AND &END_DATE)
    ));
LENGTH IMG_PATH $100. ;
    IMG_PATH = cats("&img_url",int(ranuni(0) * 4+1 ),".png") ;
RUN;

/* Parameter 데이터 생성 */
DATA image_param_data;
    LENGTH group $50. col_name $50. name $50. value $50. ;
    /*1*/group = "PROCESS" ;  col_name = "MACHINE_CODE" ;    name="공정";            value = "&MACHINE_CODE";      OUTPUT;
    /*2*/group = "LOT" ;      col_name = "LOT_NO" ;           name="LOT NO";          value = "&LOT_NO";            OUTPUT;
    /*3*/group = "UNIT" ;     col_name = "UNIT_RANGE" ;       name="UNIT범위";        value = "&UNIT_RANGE";        OUTPUT;
    /*4*/group = "DEFECT" ;      col_name = "DEFECT_CODE" ;   name="불량코드";          value = "&DEFECT_CODE";            OUTPUT;
    /*5*/group = "CAM" ;      col_name = "CAM_NO" ;         name="카메라번호";          value = "&CAM_NO";            OUTPUT;
RUN;

/* DEFECT_CODE_LIST 매크로 변수 생성 */
DATA _NULL_;
    i=1;
    DO WHILE(SCAN("&DEFECT_CODE", i, ",") ^= "");
    	IF i = 1 THEN DO;
    		DEFECT_CODE_LIST =  CATS("'",SCAN("&DEFECT_CODE", i, ","),"'");
    	END;
    	ELSE DO ;
    		DEFECT_CODE_LIST =  CATS(DEFECT_CODE_LIST,",'",scan("&DEFECT_CODE", i, ","),"'");
    	END;
    i+1;
    END;
    CALL SYMPUTX("DEFECT_CODE_LIST", DEFECT_CODE_LIST);
    CALL SYMPUTX("CAM_NO_MODEL_IMAGE", CATS("&model_img_path", "&CAM_NO_ZERO",".jpg"));
RUN;

%PUT DEFECT_CODE = &DEFECT_CODE;
%PUT DEFECT_CODE_LIST = &DEFECT_CODE_LIST;
%PUT CAM_NO_MODEL_IMAGE = &CAM_NO_MODEL_IMAGE;


PROC SQL NOPRINT;
	SELECT
          '<div style="display:inline"><div class="imgbox">'|| STRIP(&g_defect_name) ||'<br> <img src="'|| STRIP(IMG_PATH) ||'" />'|| STRIP(image_pos_x) ||','|| STRIP(image_pos_y) ||'</div></div>'
                AS IMG_LIST
    INTO
    	  : img_list SEPARATED BY ' '
    FROM WORK.IMAGE_DATA_TEMP
    WHERE 1=1
    AND &g_defect_code IN ( &DEFECT_CODE_LIST)
    AND x  BETWEEN (&parm_x - &parm_range_x) AND (&parm_x + &parm_range_x)
    AND y  BETWEEN (&parm_y - &parm_range_y) AND (&parm_y + &parm_range_y)
    ;
QUIT;

/* webout style  */
DATA _null_;
    FILE _webout;
    input line : $32762. ;
    line = resolve(_infile_) ;
    put line;
    cards4 ;
<html>
<head>
<style>
 #leftmenu{
    text-align: center;
}
.imgbox {
    float: left;
    text-align: center;
	font-size: 14px;
    width: 170px;
    border: 2px solid gray;
    margin: 4px;
    padding: 6px;
}
</style>
<body>
<div id="leftmenu" >
<h3> 불량 상세 이미지 </h3>
&img_list
</div>
<div>
;;;;
RUN;

%MACRO RENDERING;
    %STPBEGIN;
    /* GPLOT STYLE INCLUDE */
    %fun_GPLOT_STYLE;
    PROC SQL;
        CREATE VIEW WORK.SCATTER_PLOT  AS
        	SELECT
                  x AS image_pos_x
                , y AS image_pos_y
                , UNIT_NO
                , &g_defect_name AS BAD_NAME
            FROM WORK.IMAGE_DATA_TEMP
            WHERE 1=1
            AND &g_defect_code IN ( &DEFECT_CODE_LIST)
            AND x  BETWEEN (&parm_x - &parm_range_x) AND (&parm_x + &parm_range_x)
            AND y  BETWEEN (&parm_y - &parm_range_y) AND (&parm_y + &parm_range_y)
            ;
    QUIT;

    DATA WORK._HTMLDATA_; SET WORK.SCATTER_PLOT;
        length myhtml $1000;
        myhtml=' title='|| quote(
        'image_pos_x :' || image_pos_x || '0D'x || 'image_pos_y:' || image_pos_y || '0D'x || 'Unit No:' || UNIT_NO || '0D'x  || '불량명:' || BAD_NAME || '0D'x  )|| ' target="_self_"'
        ;
    RUN;

    PROC GPLOT DATA=WORK._HTMLDATA_
    NOCACHE ;

    PLOT image_pos_y * image_pos_x = BAD_NAME /
    	VAXIS=AXIS1
    	HAXIS=AXIS2
        FRAME html=myhtml
        iframe="&CAM_NO_MODEL_IMAGE"
        IMAGESTYLE=fit
    ;
    RUN; QUIT;

    ods _all_ close;
    %STPEND;

%MEND;


%let _GOPTIONS=%str(xpixels=1000 ypixels=700 transparency);
%RENDERING;

/* 마지막 코드 */
DATA _null_;
  FILE _webout;
  input line : $32762. ;
  line = resolve(_infile_) ;
  put line;
  cards4 ;
</div>
</body>
</html>
;;;;
RUN;
