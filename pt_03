-------INSP_MATCHING_SEARCH

%INCLUDE "&STP_HOME.&SEP_DLM.stp_autoexec.sas";
%LET LOTID = MPH3AJ2AA;

DATA WORK.INSP_MATCHING_01(KEEP=MACHINE_CODE MODEL_ROW UNIT_NO CHANGE_DEFECT_NAME NO  ) ;
SET VA_DATA.VA_V_DEFECTIMAGEINFO(WHERE=(
    MACHINE_CODE NOT IN("DAOI_01")
    /*AND LOTID = "&LOTID"*/
	AND  INDEX(LOTID,  SUBSTR("&LOTID", 1, LENGTH("&LOTID")-1)  ) >0
    ));
LENGTH MACHINE_NAME $10.;
	MACHINE_NAME = SCAN(MACHINE_CODE, 1, "_") ;

	IF MACHINE_NAME = "AVI" THEN DO;
		MODEL_ROW  = SUBSTR(LOTID, LENGTH(LOTID));
	END;

	SELECT (MACHINE_NAME) ;
		WHEN ("PAOI")  	NO =1;
		WHEN ("AEI")  	NO =2;
		WHEN ("AVI")  	NO =3;
		OTHERWISE;
	END;

	MACHINE_CODE = CATS(MACHINE_NAME,"_",MODEL_ROW);
	LOTID = SUBSTR(LOTID, 1, LENGTH(LOTID)-1);
RUN;

PROC SORT DATA= WORK.INSP_MATCHING_01(KEEP=UNIT_NO) OUT=WORK.INSP_MATCHING_UNIT_NO NODUPKEY;
BY UNIT_NO ;
RUN;

PROC SORT DATA= WORK.INSP_MATCHING_01(KEEP=MACHINE_CODE NO) OUT=WORK.INSP_MATCHING_MACHINE_CODE NODUPKEY;
BY MACHINE_CODE NO ;
RUN;

DATA WORK.INSP_MATCHING_02;
SET WORK.INSP_MATCHING_UNIT_NO ;
LENGTH MODEL_ROW $2. ;
	 DO i = 1 TO N ;
		SET INSP_MATCHING_MACHINE_CODE POINT=i NOBS=N ;
		MODEL_ROW = SCAN(MACHINE_CODE, 2, "_");
		OUTPUT;
	END;
RUN;


PROC SQL NOPRINT;
	CREATE TABLE WORK.INSP_MATCHING_03 AS
		SELECT
				t1.*
				, t2.CHANGE_DEFECT_NAME
		FROM WORK.INSP_MATCHING_02 t1
		LEFT JOIN WORK.INSP_MATCHING_01 t2
		ON t1.UNIT_NO = t2.UNIT_NO
		AND t1.MACHINE_CODE = t2.MACHINE_CODE
		ORDER BY
				t1.UNIT_NO
				, t1.MODEL_ROW
				, t1.NO
		;
QUIT;



DATA WORK.INSP_MATCHING_04;
SET WORK.INSP_MATCHING_03;
BY UNIT_NO MODEL_ROW NO ;
LENGTH BF_DEFECT_NAME $20. ;
RETAIN BF_NO 0
	BF_DEFECT_NAME ""
;
IF FIRST.MODEL_ROW THEN DO;
	BF_NO = NO ;
	BF_DEFECT_NAME = CHANGE_DEFECT_NAME ;
END;
ELSE DO;
	IF NO > BF_NO AND CHANGE_DEFECT_NAME = "" AND BF_DEFECT_NAME ^= ""  THEN DO;
		CHANGE_DEFECT_NAME ="고정불량";
		BF_NO = NO;
	END;
	ELSE DO;
		BF_NO = NO;
	END;
END;
RUN;


PROC SORT DATA=WORK.INSP_MATCHING_04 NODUPKEY ;
BY UNIT_NO  NO MACHINE_CODE CHANGE_DEFECT_NAME   ;
RUN;


PROC TRANSPOSE DATA=WORK.INSP_MATCHING_04  OUT = WORK.INSP_MATCHING_05(DROP=_NAME_ _LABEL_) ;
BY  UNIT_NO ;
ID MACHINE_CODE ;
VAR CHANGE_DEFECT_NAME;
RUN;


DATA WORK.INSP_MATCHING_LIST(DROP=UNIT_NO) ;
RETAIN LOTID UNIT;
SET WORK.INSP_MATCHING_05 ;
LENGTH LOTID $20. UNIT $100.;
    LOTID = "&LOTID" ;
    UNIT = '<A href="javascript:img_link('''||STRIP(LOTID)||''', '''||STRIP(UNIT_NO)||''')" >'||UNIT_NO||'</A>' ;
   /*UNIT = '<div onClick="img_link('''||STRIP(LOTID)||''', '''||STRIP(UNIT_NO)||''')" >'||UNIT_NO||'</div>' ;*/

LABEL
    LOTID='LOT'
    UNIT='Unit번호'
;
RUN;

PROC SQL NOPRINT;
	DROP TABLE WORK.INSP_MATCHING_01 ;
	DROP TABLE WORK.INSP_MATCHING_02 ;
	DROP TABLE WORK.INSP_MATCHING_03 ;
	DROP TABLE WORK.INSP_MATCHING_04 ;
	DROP TABLE WORK.INSP_MATCHING_05 ;
QUIT;


DATA _null_;
    FILE _webout;
    input line : $32762. ;
    line = resolve(_infile_) ;
    put line;
    cards4 ;
<html>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
<style>
 #wrap{width:1200px;}
 #model_img{height:900px; width:1150px; float:left;}
 #matc_img{background:#FFFFFF; height:900px;  width:700px; float:left;  text-align: left;}
 #sematc_img{background:#FFFFFF; height:500px;  width:550px; float:left;  text-align: center;}
.defect_group_box {
    background:#FFCCFF;
	width : 100% ;
    height : 100%;
	margin : 10px;
}
.imgbox {
    float: left;
    text-align: center;
	font-size: 14px;
    width: 170px;
    border: 2px solid gray;
    margin: 2px;
    padding: 1px;
}
</style>
<script type="text/javascript">

    function img_link(lot_id, unit_no){
        window.open("&stp_matimg_path","pop", "width=700, height=800, left=650 , toolbar=no, menubar=no, resizable=yes, scrollbars=yes" ) ;
        document.forms["boxform"]._program.value="&stp_matimg_path";
        document.forms["boxform"].target="pop";
        document.forms["boxform"].LOTID.value = lot_id ;
        document.forms["boxform"].UNIT_NO.value = unit_no;
        document.forms["boxform"].submit();
    }

</script>
</head>
<body>
<form name="boxform" method="get" action= "&_URL">
    <input name="_program" value="&_PROGRAM" type="hidden">
    <input name="MACHINE_CODE" value="&MACHINE_CODE" type="hidden">
    <input name="LOTID" value="&LOTID" type="hidden">
    <input name="UNIT_NO"  type="hidden">
</from>
<div id="matc_img">
<h1> 검사 맵 매칭 </h1>
;;;;
RUN;

options odsstyle=Sapphire;
%STPBEGIN;
/*
PROC REPORT DATA=WORK.INSP_MATCHING_LIST;
    column LOTID UNIT PAOI_A PAOI_B  AVI_A AVI_B ;
RUN;
*/

PROC PRINT DATA=WORK.INSP_MATCHING_LIST LABEL;
RUN;

ods _all_ close;
%STPEND;


/* 마지막 코드 */
DATA _null_;
  FILE _webout;
  input line : $32762. ;
  line = resolve(_infile_) ;
  put line;
  cards4 ;
</div>
</body>
</html>
;;;;
RUN;



-----INSP_MATCHING_IMG_LIST

%INCLUDE "&STP_HOME.&SEP_DLM.stp_autoexec.sas";


/* 기준 데이터 생성 */
PROC SQL NOPRINT;
    CREATE TABLE WORK.VA_V_DEFECT_MATC_01 AS
        SELECT
              MACHINE_CODE
            , LOTID
            , MODEL_ROW
            , CHANGE_DEFECT_NAME
            , CAM_NO
            , IMAGE_POS_X
            , IMAGE_POS_Y
            , IMAGE_FILENAME
        FROM VA_DATA.VA_V_DEFECTIMAGEINFO
        WHERE 1=1
            AND MACHINE_CODE ^= "DAOI_01"
            AND INDEX(LOTID,  SUBSTR("&LOTID", 1, LENGTH("&LOTID")-1)  ) >0
            AND UNIT_NO  = "&UNIT_NO"
        ;
QUIT;


DATA WORK.VA_V_DEFECT_MATCHING(KEEP=MACHINE_CODE LOTID MODEL_ROW CHANGE_DEFECT_NAME IMG_LIST) ;
SET WORK.VA_V_DEFECT_MATC_01;
LENGTH  IMG_PATH $250. MACHINE_NAME $100. ;
    MACHINE_NAME = SCAN(MACHINE_CODE, 1, "_");
    IF MACHINE_NAME ^= "AVI" THEN DO;
        MACHINE_NAME = CATS(SUBSTR(MACHINE_NAME,1,1),"-",SUBSTR(MACHINE_NAME,2)) ;
    END;

    IF LOTID = "MPH3AJ2AA" THEN DO;
        IMG_PATH = cat("&img_base_url",trim(MACHINE_NAME),"/",trim(LOTID),"/",urlencode(trim(IMAGE_FILENAME)),".jpg") ;
    END ;
    ELSE DO;
        IMG_PATH = cats("&img_url",int(ranuni(0) * 4+1 ),".png") ;
    END;
    IMG_LIST = '<div class="imgbox" > <img src="'|| STRIP(IMG_PATH) ||'" />'|| STRIP(image_pos_x) ||','|| STRIP(image_pos_y) ||'</div>';

LABEL
    IMG_LIST = "불량이미지"
;

RUN;

PROC SORT DATA = WORK.VA_V_DEFECT_MATCHING ;
BY MACHINE_CODE LOTID ;
RUN;


options odsstyle=Sapphire;
%STPBEGIN;

PROC PRINT DATA=WORK.VA_V_DEFECT_MATCHING LABEL ;
RUN;

ods _all_ close;
%STPEND;
